buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.3.1'
    }
}

apply plugin: "com.jfrog.bintray"
apply plugin: "maven-publish"
apply plugin: "java"

apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'



group = 'com.developerb.nmxlp' // Yeah I know about the typo, but hard to change now that it's in JCenter..
version = '1.4.0'

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    options.encoding = 'UTF-8'
    options.compilerArgs = [
            "-Xlint:varargs", "-Xlint:cast", "-Xlint:classfile",
            "-Xlint:dep-ann", "-Xlint:divzero", "-Xlint:empty", "-Xlint:finally",
            "-Xlint:overrides", "-Xlint:path", "-Xlint:processing", "-Xlint:static",
            "-Xlint:fallthrough", "-Xlint:rawtypes",
            "-Xlint:unchecked", "-Xlint:-options", "-Werror"
    ]
}

compileTestJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    options.encoding = 'UTF-8'
    options.compilerArgs = [
            "-Xlint:varargs", "-Xlint:cast", "-Xlint:classfile",
            "-Xlint:dep-ann", "-Xlint:divzero", "-Xlint:empty", "-Xlint:finally",
            "-Xlint:overrides", "-Xlint:path", "-Xlint:processing", "-Xlint:static",
            "-Xlint:-fallthrough", "-Xlint:-rawtypes", "-Xlint:-deprecation",
            "-Xlint:-unchecked", "-Xlint:-options"
    ]
}

repositories {
    jcenter()
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

dependencies {
    implementation 'com.google.guava:guava:28.0-jre'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.2'
    testImplementation 'org.assertj:assertj-core:3.13.2'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

test {
    useJUnitPlatform()
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }
        }
    }
}


if (System.getenv("BINTRAY_USER") && System.getenv("BINTRAY_KEY")) {
    bintray {
        user = System.getenv("BINTRAY_USER")
        key = System.getenv("BINTRAY_KEY")

        publications = [ 'mavenJava' ]

        dryRun = false
        publish = true

        pkg { //package will be created if does not exist
            repo = 'maven'
            name = 'nmxmlp'
            desc = 'Simple Java-library for extracting and updating small-ish xml documents'

            websiteUrl = 'https://github.com/kimble/nmxmlp'
            issueTrackerUrl = 'https://github.com/kimble/nmxmlp/issues'
            vcsUrl = 'https://github.com/kimble/nmxmlp.git'
            githubRepo = 'kimble/nmxmlp'
            githubReleaseNotesFile = 'README.md'

            licenses = ['Apache-2.0']
            labels = ['java', 'xml', 'library']

            publicDownloadNumbers = true
        }
    }
}
else {
    logger.debug("Set bintrayUser and bintrayKey in ~/.gradle/gradle.properties to enable bintray upload")
}
