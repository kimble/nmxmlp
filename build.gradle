buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.1'
    }
}

apply plugin: "com.jfrog.bintray"
apply plugin: "maven-publish"
apply plugin: "java"



group = 'com.developerb.nmxlp' // Yeah I know about the typo, but hard to change now that it's in JCenter..
version = '0.9.4'

compileJava {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    options.encoding = 'UTF-8'
    options.compilerArgs = [
            "-Xlint:varargs", "-Xlint:cast", "-Xlint:classfile",
            "-Xlint:dep-ann", "-Xlint:divzero", "-Xlint:empty", "-Xlint:finally",
            "-Xlint:overrides", "-Xlint:path", "-Xlint:processing", "-Xlint:static",
            "-Xlint:fallthrough", "-Xlint:rawtypes",
            "-Xlint:unchecked", "-Xlint:-options", "-Werror"
    ]
}

compileTestJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    options.encoding = 'UTF-8'
    options.compilerArgs = [
            "-Xlint:varargs", "-Xlint:cast", "-Xlint:classfile",
            "-Xlint:dep-ann", "-Xlint:divzero", "-Xlint:empty", "-Xlint:finally",
            "-Xlint:overrides", "-Xlint:path", "-Xlint:processing", "-Xlint:static",
            "-Xlint:-fallthrough", "-Xlint:-rawtypes", "-Xlint:-deprecation",
            "-Xlint:-unchecked", "-Xlint:-options"
    ]
}

repositories {
    jcenter()
}

dependencies {
    compile 'com.google.guava:guava:18.0'

    testCompile 'junit:junit:4.12',
                'org.assertj:assertj-core:1.7.1'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayKey

    publications = [ 'mavenJava' ] // see publications closure

    pkg { //package will be created if does not exist
        repo = 'maven'
        name = 'nmxmlp'
        desc = 'Simple Java-library for extracting and updating small-ish xml documents'

        websiteUrl = 'https://github.com/kimble/nmxmlp'
        issueTrackerUrl = 'https://github.com/kimble/nmxmlp/issues'
        vcsUrl = 'https://github.com/kimble/nmxmlp.git'

        licenses = ['Apache-2.0']
        labels = ['java', 'xml', 'library']

        publicDownloadNumbers = true
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}
